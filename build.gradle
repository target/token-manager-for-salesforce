buildscript {
  ext {
    // libraries
    springBootVersion = '2.5.3'
    lombokVersion = '1.18.20'
    apacheCommonsVersion = '3.12.0'
    hibernateValidatorVersion = '6.1.7.Final'
    mockWebserverVersion = '4.9.1'
    spotbugsAnnotationsVersion = '4.5.2'
    httpComponentsVersion = '4.5.13'
    reactorTestVersion = '3.4.8'

    // plugins
    spotlessPluginVersion = '5.14.2'
    dependencyManagementPluginVersion = '1.0.11.RELEASE'
    dependencyUpdatesPluginVersion = '0.39.0'
    jacocoPluginVersion = '0.8.7'
    libraryGradlePluginVersion = '2.1.0'
    qualityPluginVersion = '4.6.0'
  }
  repositories {
    mavenCentral()
    google()
  }
}

plugins {
  id 'java-library'
  id 'idea'
  id 'jacoco'
  id 'maven-publish'
  id 'com.github.ben-manes.versions' version "${dependencyUpdatesPluginVersion}"
  id 'com.diffplug.spotless' version "${spotlessPluginVersion}"
  id 'io.spring.dependency-management' version "${dependencyManagementPluginVersion}"
  id 'ru.vyarus.quality' version "${qualityPluginVersion}"
}

apply from: 'gradle/spotless.gradle'

subprojects {
  group 'com.tgt.crm'
  sourceCompatibility = 11

  repositories {
    mavenCentral()
    google()
  }

  jar {
    enabled = true
  }

  apply plugin: 'java-library'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'jacoco'
  apply plugin: 'idea'
  apply plugin: 'maven-publish'
  apply plugin: 'ru.vyarus.quality'
  apply plugin: 'com.diffplug.spotless'

  apply from: '../gradle/spotless.gradle'
  apply from: '../gradle/checks.gradle'

  publishing {
    publications {
      gpr(MavenPublication) {
        from(components.java)
      }
    }
    repositories {
      maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/target/token-manager-for-salesforce"
        credentials {
          username = System.getenv("GITHUB_ACTOR")
          password = System.getenv("GITHUB_TOKEN")
        }
      }
    }
  }

  dependencyManagement {
    imports {
      mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }
  }

  test {
    // use junit5
    useJUnitPlatform()
  }

  dependencies {
    // used to copy & log headers on requests if debug enabled
    implementation "org.apache.commons:commons-lang3:${apacheCommonsVersion}"
    // used for bean validation
    implementation "org.hibernate.validator:hibernate-validator:${hibernateValidatorVersion}"
    implementation "org.apache.httpcomponents:httpclient:${httpComponentsVersion}"
    implementation 'io.micrometer:micrometer-core'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "com.squareup.okhttp3:mockwebserver:${mockWebserverVersion}"
    testImplementation "com.squareup.okhttp3:okhttp:${mockWebserverVersion}"
  }
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}
